apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'org.github.ayltai.mopub.adapter.app'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName '1.0.0'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries false
    }
}

checkstyle {
    configFile     rootProject.file('checkstyle.xml')
    showViolations true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':adapters')

    debugCompile   'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}

task customFindBugs(type: FindBugs) {
    ignoreFailures = false
    effort         = "max"
    reportLevel    = "medium"
    classes        = files("$project.buildDir/intermediates/classes")

    // Use this only if you want exclude some errors
    excludeFilter = file("$rootProject.rootDir/findbugs-android-exclude.xml")

    source    = fileTree('src/main/java/')
    classpath = files()

    reports {
        xml.enabled      = false
        xml.withMessages = true
        html.enabled     = true
        xml.destination  "$project.buildDir/outputs/findbugs/findbugs-output.xml"
        html.destination "$project.buildDir/outputs/findbugs/findbugs-output.html"
    }
}

assemble.dependsOn customFindBugs
